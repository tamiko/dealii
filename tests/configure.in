dnl $Id$
dnl
dnl configure script to find the main deal library
dnl in .. or ../deal.II or ../deal
dnl

define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl
AC_INIT(base)

dnl Supply deal-directory in command line or find it, if it hasn't
dnl been set as an environment variable already
AC_ARG_WITH(deal,
  [  --with-deal=DIR DIR is the root of the deal.II library],
  [  DEAL_II_DIR=$withval],
  [
    if test "x$DEAL_II_DIR" = "x" ; then
      if test -r ../common/Make.global_options ; then
        DEAL_II_DIR=`pwd`/.. ;
      fi
      if test -r ../deal.II/common/Make.global_options ; then
        DEAL_II_DIR=`pwd`/../deal.II ;
      fi
      if test -r ../deal/common/Make.global_options ; then
        DEAL_II_DIR=`pwd`/../deal ;
      fi
    fi
  ])
AC_SUBST(DEAL_II_DIR)


dnl Check whether we can use "test -L" and if not substitute it with
dnl "bash test -L". Unfortunately, the systems where either works seem
dnl mutually exclusive
rm -f acconfig.link
AC_MSG_CHECKING(for test -L)
ln -s Makefile acconfig.link
echo "test -L acconfig.link" > acconfig.test
chmod 777 acconfig.test
if eval ./acconfig.test ; then
  TEST_MINUS_L="test -L" ;
else
  TEST_MINUS_L="bash test -L" ;
fi
rm -f acconfig.link acconfig.test
AC_MSG_RESULT($TEST_MINUS_L)
AC_SUBST(TEST_MINUS_L)



dnl Check whether we can call 'ulimit' on a system. Some systems do
dnl not appear to allow this, for whatever reason.
AC_MSG_CHECKING(whether we can call ulimit)
(ulimit -t 2400 2>&1) > /dev/null
if test ! $? = 0 ; then
  AC_MSG_RESULT(no)
  ULIMIT=":"
else
  AC_MSG_RESULT(yes)
  ULIMIT="ulimit"
fi
AC_SUBST(ULIMIT)



AC_OUTPUT(Makefile.paths)
